//
// Weather Maker for Unity
// (c) 2016 Digital Ruby, LLC
// Source code may be used for personal or commercial projects.
// Source code may NOT be redistributed or sold.
// 
// *** A NOTE ABOUT PIRACY ***
// 
// If you got this asset off of leak forums or any other horrible evil pirate site, please consider buying it from the Unity asset store at https ://www.assetstore.unity3d.com/en/#!/content/60955?aid=1011lGnL. This asset is only legally available from the Unity Asset Store.
// 
// I'm a single indie dev supporting my family by spending hundreds and thousands of hours on this and other assets. It's very offensive, rude and just plain evil to steal when I (and many others) put so much hard work into the software.
// 
// Thank you.
//
// *** END NOTE ABOUT PIRACY ***
//

// https://alastaira.wordpress.com/2014/12/30/adding-shadows-to-a-unity-vertexfragment-shader-in-7-easy-steps/

Shader "WeatherMaker/WeatherMakerMoonShader"
{
	Properties
	{
		_MainTex("Moon Texture", 2D) = "white" {}
	}
	SubShader
	{
		Tags { "Queue" = "Background+1" "RenderType" = "Background" "IgnoreProjector" = "True" "PerformanceChecks" = "False" "LightMode" = "Always" }
		Cull Back Lighting Off ZWrite Off ZTest LEqual Blend SrcAlpha OneMinusSrcAlpha

		CGINCLUDE

		#include "WeatherMakerSkyShader.cginc"

		#pragma target 3.0
		#pragma fragmentoption ARB_precision_hint_fastest
		#pragma glsl_no_auto_normalization
		#pragma multi_compile_instancing
		#pragma multi_compile __ UNITY_HDR_ON

		sampler2D _WeatherMakerSkySphereTexture;
		int _MoonIndex;

		struct vertexOutput
		{
			float4 pos: SV_POSITION;
			float3 normalWorld: NORMAL;
			float2 tex: TEXCOORD0;
			float4 grabPos: TEXCOORD1;
			float4 ray : TEXCOORD2;
			WM_BASE_VERTEX_TO_FRAG
		};

		vertexOutput vert(appdata_base v)
		{
			WM_INSTANCE_VERT(v, vertexOutput, o);
			o.normalWorld = normalize(WorldSpaceVertexPosNear(v.normal));
			o.pos = UnityObjectToClipPosFarPlane(v.vertex);
			o.tex = TRANSFORM_TEX(v.texcoord, _MainTex);
			o.grabPos = ComputeGrabScreenPos(o.pos);
			o.ray.xyz = -WorldSpaceViewDir(v.vertex);
			o.ray.w = pow(_WeatherMakerNightMultiplier, 3.0);
			return o;
		}

		fixed4 frag(vertexOutput i) : SV_TARGET
		{
			WM_INSTANCE_FRAG(i);

			i.ray.xyz = normalize(i.ray.xyz);

			if (WM_ENABLE_SKY_SUN_ECLIPSE)
			{
				fixed lerpSun = CalcSunSpot(_WeatherMakerSunVar1.x * 144, _WeatherMakerSunDirectionUp, i.ray.xyz);
				fixed feather = saturate(dot(-i.ray.xyz, i.normalWorld) * 3.0);
				return fixed4(0.0, 0.0, 0.0, lerpSun * feather);
			}
			else
			{
				fixed4 moonColor = tex2D(_MainTex, i.tex.xy);
				fixed lightNormal = max(0.0, dot(i.normalWorld, _WeatherMakerSunDirectionUp));
				fixed3 lightFinal = _WeatherMakerSunColor.rgb * lightNormal * _WeatherMakerMoonTintColor[_MoonIndex].rgb * _WeatherMakerMoonTintColor[_MoonIndex].a;
				fixed lightMax = max(lightFinal.r, max(lightFinal.g, lightFinal.b));
				fixed feather = pow(abs(dot(i.ray.xyz, i.normalWorld)), 0.2);

				// alpha ramps up as night approaches or if the moon is lit by another light source
				moonColor.a = max(i.ray.w, lightMax);

				// apply sun light
				moonColor.rgb *= lightFinal;

				return moonColor;
			}
		}

		ENDCG

		Pass
		{
			Tags { "LightMode" = "Always" }

			CGPROGRAM

			#pragma vertex vert
			#pragma fragment frag
			
			ENDCG
		}
	}

	FallBack Off
}